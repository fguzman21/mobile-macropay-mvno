// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.google.dagger:hilt-android-gradle-plugin:2.49"
        classpath 'com.google.gms:google-services:4.3.14'
    }
}

plugins {
    id 'com.android.application' version '8.3.2' apply false
    id 'com.android.library' version '8.3.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.24' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.24' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.9.24' apply false
    id 'com.google.dagger.hilt.android' version '2.48' apply false
    id 'com.google.gms.google-services' version '4.3.14' apply false
    id 'com.google.firebase.crashlytics' version '2.9.1' apply false
    id 'com.google.devtools.ksp' version '1.9.24-1.0.20' apply false
    id 'io.gitlab.arturbosch.detekt' version '1.23.3'
}

// Configuraci√≥n de archivos y tareas de Detekt
def projectSource = file(projectDir)
def configFile = files("$rootDir/config/detekt/detekt.yml")
def baselineFile = file("$rootDir/config/detekt/baseline.xml")
def kotlinFiles = "**/*.kt"
def resourceFiles = "**/resources/**"
def buildFiles = "**/build/**"

apply plugin: 'io.gitlab.arturbosch.detekt'

tasks.register("detektAll", io.gitlab.arturbosch.detekt.Detekt) {
    def autoFix = project.hasProperty('detektAutoFix')
    description = "Custom DETEKT build for all modules"
    parallel = true
    ignoreFailures = false
    autoCorrect = autoFix
    buildUponDefaultConfig = true
    setSource(projectSource)
    baseline.set(baselineFile)
    config.setFrom(configFile)
    include(kotlinFiles)
    exclude(resourceFiles, buildFiles)
    reports {
        html.enabled = true
        xml.enabled = true
        txt.enabled = false
    }
}

tasks.register("detektGenerateBaseline", io.gitlab.arturbosch.detekt.DetektCreateBaselineTask) {
    description = "Custom DETEKT build to build baseline for all modules"
    parallel = true
    ignoreFailures = false
    buildUponDefaultConfig = true
    setSource(projectSource)
    baseline.set(baselineFile)
    config.setFrom(configFile)
    include(kotlinFiles)
    exclude(resourceFiles, buildFiles)
}
